apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.5

def artifactId = projectDir.name
group = 'edu.cmu.sei.ams.cloudlet'
def groupId = group
version = '1.7.2'

wrapper {
    gradleVersion = '2.3'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile (
        'edu.cmu.sei.ams.cloudlet:client-lib:1.13.3',
        'com.google.android:android:4.1.1.4'
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

// Depends on mvn being installed on the machine, and having a local /.m2 repo. Works only on Windows for now.
task publishLocal(type:Exec) {
    executable 'mvn.bat'
    args 'install:install-file', '-Dfile=build/libs/' + artifactId + '-' + version + '.jar', '-DgroupId=' + groupId, '-DartifactId=' + artifactId, '-Dversion=' + version, '-Dpackaging=jar'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: hasProperty('ossrhUsername')?ossrhUsername:'', password: hasProperty('ossrhPassword')?ossrhPassword:'')
      }

      pom.project {
        name 'Cloudlet Client Android Library'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'A library adding Android support for communicating with cloudlets from an Android app.'
        url 'https://github.com/SEI-AMS/client-lib-android'

        scm {
          connection 'scm:git:git@github.com:SEI-AMS/client-lib-android.git'
          developerConnection 'scm:git:git@github.com:SEI-AMS/client-lib-android.git'
          url 'git@github.com:SEI-AMS/client-lib-android.git'
        }

        licenses {
          license {
            name 'KD-Cloudlet license'
            url 'https://github.com/SEI-AMS/client-lib-android/blob/master/LICENSE'
          }
        }

        developers {
          developer {
            name 'Sebastian Echeverria'
            email 'secheverria@sei.cmu.edu'
            organization 'Software Engineering Institute'
            organizationUrl 'https://github.com/SEI-AMS'
          }
        }
      }
    }
  }
}